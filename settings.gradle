pluginManagement {
  repositories {
    maven { url System.getenv('REPOSITORY_MANAGER_URL') }
    gradlePluginPortal()
  }
}

include 'omar-services-app'

project(':omar-services-app').projectDir = "${rootProject.projectDir}/apps/omar-services-app" as File

if ( System.getenv('O2_INLINE_BUILD')) {
  def modules = [
      "omar-core",
      "omar-download",
      "omar-geoscript",
      "omar-hibernate-spatial",
      "omar-oms",
      "omar-mensa",
      "omar-raster",
      "omar-opir",
      "omar-stager-core",
      "omar-superoverlay",
      "omar-video",
      "omar-wcs",
      "omar-wfs",
      "omar-wms",
      "omar-wmts"
  ]

  modules.each { module ->
    include "${module}-plugin"

    project(":${module}-plugin").projectDir = "${rootProject.projectDir}/../${module}/plugins/${module}-plugin" as File
  }
}
