pluginManagement {
	repositories {
		mavenLocal()
		maven { url ossimMavenProxy }
		gradlePluginPortal()
	}
}

rootProject.name='omar-services'

[ 'lib', 'plugin', 'app' ].each { String projectType ->
	File projectTypeDir = new File( rootDir, "${ projectType }s" )

	if ( projectTypeDir.exists() )
	{
		projectTypeDir.eachDirMatch( { it.endsWith( projectType ) } ) { File subprojectDir ->
			include subprojectDir.name

			project( ":${ subprojectDir.name }" ).projectDir = subprojectDir
		}
	}
}

if ( System.getenv( 'O2_INLINE_BUILD' ) )
{
	def modules = [ 
      'omar-core',
       'omar-download',
      'omar-geoscript',
      'omar-hibernate-spatial',
      'omar-oms',
       'omar-mensa',
       'omar-raster',
      'omar-stager-core',
       'omar-superoverlay',
       'omar-video',
       'omar-wcs',
      'omar-wfs',
      'omar-wms',
       'omar-wmts'
	]

	modules.each { module ->
		include "${ module }-plugin"

		project( ":${ module }-plugin" ).projectDir = "${ rootProject.projectDir }/../${ module }/plugins/${ module }-plugin" as File
	}
}
