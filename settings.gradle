rootProject.name = 'omar-services'

[ 'lib', 'plugin', 'app' ].each { String projectType ->
    File projectTypeDir = new File( rootDir, "${ projectType }s" )

    if ( projectTypeDir.exists() ) {
        projectTypeDir.eachDirMatch( { it.endsWith( projectType ) } ) { File subprojectDir ->
            include subprojectDir.name
            project( ":${ subprojectDir.name }" ).projectDir = subprojectDir
        }
    }
}

if ( System.getenv( 'O2_INLINE_BUILD' ) ) {
    def modules = [
        'omar-geoscript',
        'omar-raster',
        'omar-wfs',
        'omar-wms',
    ]

    modules?.each { module ->
        includeBuild  "${rootProject.projectDir.parent}/${module}"
    }
}    
